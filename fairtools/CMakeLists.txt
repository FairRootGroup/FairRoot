################################################################################
# Copyright (C) 2014-2019 GSI Helmholtzzentrum fuer Schwerionenforschung GmbH  #
#                                                                              #
#              This software is distributed under the terms of the             #
#              GNU Lesser General Public Licence (LGPL) version 3,             #
#                  copied verbatim in the file "LICENSE"                       #
################################################################################

add_subdirectory(MCStepLogger)

if(Geant3_FOUND AND Geant4VMC_FOUND AND yaml-cpp_FOUND)
  add_subdirectory(MCConfigurator)
endif()

set(target FairTools)

set(sources
  FairLogger.cxx
  FairMonitor.cxx
  FairSystemInfo.cxx
)

fair_change_extensions_if_exists(.cxx .h FILES "${sources}" OUTVAR headers)

add_library(${target} SHARED ${sources} ${headers})
fairroot_library_settings(${target})

target_include_directories(${target} PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
  $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

target_link_libraries(${target} PUBLIC
  FairLogger::FairLogger

  ROOT::Core
  ROOT::Graf
  ROOT::Hist
  ROOT::Gpad
  ROOT::RIO
  ROOT::MathCore
)

fairroot_target_root_dictionary(${target}
  HEADERS ${headers}
  LINKDEF FairToolsLinkDef.h
  EXTRA_INCLUDE_DIRS ${CMAKE_INSTALL_FULL_INCLUDEDIR}
)

fairroot_install_exported(TARGETS ${target})
install(FILES ${headers} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
